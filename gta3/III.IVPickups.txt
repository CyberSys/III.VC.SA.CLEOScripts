{$CLEO .cs}
0000:
05E5: 15@ = game_version
if or
 15@ == 0
 15@ == 1
 15@ == 2
then
//0A8C: write_memory 0x0047467D size 5 value 0x90 virtual_protect 1  //CPickups::DoPickUpEffects(CEntity *) for weapon pickups
0A8C: write_memory 0x0047459C size 5 value 0x90 virtual_protect 1  //CPickups::DoPickUpEffects(CEntity *) for money pickups

0A8C: write_memory 0x431B25 size 1 value 0xE9 virtual_protect 1
0A8C: write_memory 0x431B26 size 4 value 0x000000AF virtual_protect 1 

0A8C: write_memory 0x43179D size 5 value 0x90 virtual_protect 1 //static shadows
0A8C: write_memory 0x431863 size 5 value 0x90 virtual_protect 1 //point light
0A8C: write_memory 0x43196C size 5 value 0x90 virtual_protect 1 //coronas
end

while true
wait 0

05E5: 15@ = game_version
if
 15@ == 0
then
 9@ = 0x878C98
else
 if
  15@ == 1
 then
  9@ = 0x878C48
 else
  9@ = 0x888D88
 end 
end
0A8E: 10@ = 9@ + 0x248C // int

for 0@ = 9@ to 10@ step 0x1C
    0A8E: 5@ = 0@ + 0x10 // int
    0A8D: 1@ = read_memory 5@ size 4 virtual_protect 0  //x
    0A8E: 5@ = 0@ + 0x14 // int
    0A8D: 2@ = read_memory 5@ size 4 virtual_protect 0  //y
    0A8E: 5@ = 0@ + 0x18 // int
    0A8D: 3@ = read_memory 5@ size 4 virtual_protect 0  //z
    0A8E: 5@ = 0@ + 0x4 // int 
    0A8D: 6@ = read_memory 5@ size 4 virtual_protect 0  //6@ - [POINTER] - CObjectVC* pObject
    0A8E: 5@ = 0@ + 0x0C // int
    0A8D: 7@ = read_memory 5@ size 2 virtual_protect 0 //7@ - [WORD] - wModelId
      if
       6@ <> 0
      then
       0AEC: 8@ = object_struct 6@ handle
        if
         03CA:   object 8@ exists
        then
         if
          00EC:   actor $PLAYER_ACTOR sphere 0 near_point 1@ 2@ radius 60.0 60.0
         then
          if or
           00EC:   actor $PLAYER_ACTOR sphere 0 near_point 1@ 2@ radius 45.0 45.0
           02CC:   object 8@ bounding_sphere_visible
          then
           02CE: 4@ = ground_z 1@ 2@ 3@
           inc(4@, 0.09)
           0A8F: 13@ = 3@ - 4@ // int
             if
              13@ <= 5.0
             then
              0A8E: 5@ = 6@ + 0x3C // int
              0A8C: write_memory 5@ size 4 value 4@ virtual_protect 0 //z
             
              0176: 15@ = object 8@ z_angle 
              0B17: 15@ _&= 0xFF
             
               if 
                15@ <> 0xFF
               then 
                0208: 15@ = random_float 0.0 360.0
                0B17: 15@ _&= 0xFF
                 if 
                  7@ <> 1323 //#MONEY
                 then
                  0453: set_object 8@ XY_rotation 90.0 0.0 angle 15@   
                 else
                  0453: set_object 8@ XY_rotation 0.0 0.0 angle 15@
                 end
               end
               dec(4@, 0.05)
               0AB1: call_scm_func @createParticleForWeapon params_count 4 wModelId 7@ coords 1@ 2@ 4@ 
             end 
          end
         end
        end
      end
end // for

end

:createParticleForWeapon
0605: 4@ = model 0@ weapon id
if
 4@ == -1
then
 4@ = 11
end

05E5: 15@ = game_version
if
 15@ == 0
then
 4@ += 0x5EDEBC
else
 if
  15@ == 1
 then
  4@ += 0x5EDEBC
 else
  4@ += 0x5FAEBC
 end 
end

0A8D: 5@ = read_memory 4@ size 1 virtual_protect 0
4@ += 1
0A8D: 6@ = read_memory 4@ size 1 virtual_protect 0
4@ += 1
0A8D: 7@ = read_memory 4@ size 1 virtual_protect 0

if
 0@ == 1323 //#MONEY
then
 5@ = 0
 6@ = 155
 7@ = 0
end 
 
016F: create_particle 3 rotation_factor 0.0 size 0.5 intensity 255 color 5@ 6@ 7@ at 1@ 2@ 3@
0AB2: ret 0